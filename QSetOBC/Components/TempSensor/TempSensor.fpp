module Components {
    @ Component for reading temperature from MCP9808 sensor
    active component TempSensor {
        @ Telemetry channel to report temperature.
        telemetry TemperatureValue: F32

        @ Telemetry channel to report number of temperature checks.
        telemetry TemperatureReadings: U32

        @ Telemetry channel to report number of errors
        telemetry ErrorCount: U32

        event SensorInitError(error: U32) severity warning high \
            format "Sensor initialization error: {}"

        event I2CReadError(status: U32) severity warning high \
            format "I2C read error: 0x{}"

        @ Port receiving calls from the rate group
        async input port run : Svc.Sched

        @ Port sending calls to the i2c driver
        output port writeRead: Drv.I2cWriteRead

        @ Command to trigger a temperature read
        async command READ_TEMPERATURE

        # ##############################################################################
        #Standard AC Ports : Required for Channels, Events, Commands, and Parameters #
        ###############################################################################
        @ Port for requesting the current time
        time get port timeGet

        @ Port for sending command registrations
        command reg port cmdRegOut

        @ Port for receiving commands
        command recv port cmdIn

        @ Port for sending command responses
        command resp port cmdResponseOut

        @ Port for sending textual representation of events
        text event port logTextOut

        @ Port for sending events to downlink
        event port logOut
 
        @ Port for sending telemetry channels to downlink
        telemetry port tlmOut

        @ Port to return the value of a parameter
        param get port prmGetOut

        @ Port to set the value of a parameter
        param set port prmSetOut
    }
}